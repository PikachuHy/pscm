java_library(
    name = "pscm_java_api",
    srcs = ["dev/pscm/PSCMScheme.java"],
    visibility = ["//visibility:public"],
    deps = select({
        "@platforms//os:android": [],
        "//conditions:default": [":pscm-jni"],
    }),
)

cc_library(
    name = "pscm_java_binding",
    srcs = ["pscm_java_binding.cpp"],
    copts = ["-std=c++20"],
    visibility = ["//visibility:public"],
    deps = [
        ":copy_jni_hdr_lib",
        "//binding/c:pscm_c_api",
    ],
    alwayslink = True,
)

cc_binary(
    name = "pscm-jni",
    linkshared = True,
    deps = [":pscm_java_binding"],
)

java_test(
    name = "pscm_java_api_test",
    srcs = ["test/PSCMSchemeTest.java"],
    test_class = "test.PSCMSchemeTest",
    deps = [
        ":pscm_java_api",
        "@maven//:junit_junit",
    ],
)

java_binary(
    name = "pscm_java_api_example",
    srcs = ["example/PSCMSchemeExample.java"],
    main_class = "PSCMSchemeExample",
    deps = [
        ":pscm_java_api",
    ],
)

genrule(
    name = "copy_link_jni_md_header",
    srcs = select({
        "@platforms//os:macos": ["@bazel_tools//tools/jdk:jni_md_header-darwin"],
        "@platforms//os:linux": ["@bazel_tools//tools/jdk:jni_md_header-linux"],
        "//conditions:default": [],
    }),
    outs = ["jni_md.h"],
    cmd = "cp -f $< $@",
)

genrule(
    name = "copy_link_jni_header",
    srcs = ["@bazel_tools//tools/jdk:jni_header"],
    outs = ["jni.h"],
    cmd = "cp -f $< $@",
)

cc_library(
    name = "copy_jni_hdr_lib",
    hdrs = [
        ":copy_link_jni_header",
        ":copy_link_jni_md_header",
    ],
    includes = ["."],
)
